IamZKdevETH

medium

# Effective way of adding and Removing components  array in SetToken

## Summary
This audit report assesses the implementation of adding and removing components in the provided smart contract. The contract includes an address[] public components array for storing component addresses. The report highlights the existing code and proposes improvements to handle adding and removing components efficiently.

## Vulnerability Detail
The current implementation of adding and removing components uses an array and a mapping. While the implementation is functional, there are opportunities for improvement. The code can be optimized to handle adding and removing components more efficiently and maintain an accurate index mapping.

## Impact
The current implementation of removing components is now using AddressArrayUtils, there are better way to improve this without using for loops

## Code Snippet
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/lib/AddressArrayUtils.sol#L39
```solidity
/**
     * Finds the index of the first occurrence of the given element.
     * @param A The input array to search
     * @param a The value to find
     * @return Returns (index and isIn) for the first occurrence starting from index 0
     */
    function indexOf(address[] memory A, address a) internal pure returns (uint256, bool) {
        uint256 length = A.length;
        for (uint256 i = 0; i < length; i++) {
            if (A[i] == a) {
                return (i, true);
            }
        }
        return (uint256(-1), false);
    }

```
## Tool used
Manual Review

## Recommendation
1) To improve the efficiency and readability of adding and removing components, the following recommendations are provided:
```solidity
mapping(address => uint256) componentIndex;
```
This mapping will store the index of each component in the components array, allowing for efficient lookup and removal.

2) the _storeComponent function as follows:
```solidity
 function _storeComponent(address component) internal returns (bool) {
     uint256 arrayIndex = components.length;
  
     components.push(component);
     componentIndex[component] = arrayIndex + 1;

     emit ComponentStored(component);
     return true;
 }
```
3) the _deleteComponent function as follows:
```solidity
/**
 * @notice Delete a component from the storage.
 * @dev We are going to switch the last item in the array with the one we are replacing.
 * That way, when we pop, we are removing the correct item.
 *
 * There are two cases we need to handle:
 *  - The address we are removing is not the last address in the array.
 *  - The address is the last address in the array.
 *
 * @param component The address of the component to delete.
 * @return Returns a boolean value indicating whether the operation succeeded.
 */
function _deleteComponent(address component) internal returns (bool) {
    uint256 index = componentIndex[component];
    if (index == 0) revert NotExistComponent(component);

    uint256 arrayIndex = index - 1;
    uint256 length = components.length;
    if (arrayIndex >= length) {
        revert ArrayIndexOutOfBounds("component");
    }

    if (arrayIndex != length - 1) {
        address lastComponent = components[length - 1];
        components[arrayIndex] = lastComponent;
        componentIndex[lastComponent] = index;
    }

    components.pop();
    delete componentIndex[component];
    
    emit ComponentDeleted(component, index);
    return true;
}
```
