kn0t

medium

# Use of deprecated chainlink function: latestAnswer()

## Summary
According to Chainlink’s documentation ([API Reference](https://docs.chain.link/data-feeds/api-reference#latestanswer)), the latestAnswer function is deprecated. This function does not throw an error if no answer has been reached, but instead returns 0, possibly causing an incorrect price to be fed to the different price feeds or even a Denial of Service.

## Vulnerability Detail
```solidity
    function _createActionInfo() internal view returns(ActionInfo memory) {
        ActionInfo memory rebalanceInfo;

        // Calculate prices from chainlink. Chainlink returns prices with 8 decimal places, but we need 36 - underlyingDecimals decimal places.
        // This is so that when the underlying amount is multiplied by the received price, the collateral valuation is normalized to 36 decimals. 
        // To perform this adjustment, we multiply by 10^(36 - 8 - underlyingDecimals)
        int256 rawCollateralPrice = strategy.collateralPriceOracle.latestAnswer();
        rebalanceInfo.collateralPrice = rawCollateralPrice.toUint256().mul(10 ** strategy.collateralDecimalAdjustment);
        int256 rawBorrowPrice = strategy.borrowPriceOracle.latestAnswer();
        rebalanceInfo.borrowPrice = rawBorrowPrice.toUint256().mul(10 ** strategy.borrowDecimalAdjustment);

        rebalanceInfo.collateralBalance = strategy.targetCollateralAToken.balanceOf(address(strategy.setToken));
        rebalanceInfo.borrowBalance = strategy.targetBorrowDebtToken.balanceOf(address(strategy.setToken));
        rebalanceInfo.collateralValue = rebalanceInfo.collateralPrice.preciseMul(rebalanceInfo.collateralBalance);
        rebalanceInfo.borrowValue = rebalanceInfo.borrowPrice.preciseMul(rebalanceInfo.borrowBalance);
        rebalanceInfo.setTotalSupply = strategy.setToken.totalSupply();

        return rebalanceInfo;
    }
```
## Impact
Incorrect return price value lead to incorrect `collateralPrice`, `collateralValue`, `borrowPrice` and `borrowValue`  calculations.
which are used in functions `engage`, `getCurrentLeverageRatio`, `_getAndValidateLeveragedInfo` and `getChunkRebalanceNotional`.

## Code Snippet
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-coop-smart-contracts/contracts/adapters/AaveLeverageStrategyExtension.sol#L895
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-coop-smart-contracts/contracts/adapters/AaveLeverageStrategyExtension.sol#L897

## Tool used

Manual Review

## Recommendation
It is recommended to use Chainlink’s `latestRoundData()` function to get the price instead. It is also recommended to add checks on the return data with proper revert messages if the price is stale or the round is incomplete, for example:
```solidity
    function _createActionInfo() internal view returns(ActionInfo memory) {
        ActionInfo memory rebalanceInfo;

        // Calculate prices from chainlink. Chainlink returns prices with 8 decimal places, but we need 36 - underlyingDecimals decimal places.
        // This is so that when the underlying amount is multiplied by the received price, the collateral valuation is normalized to 36 decimals. 
        // To perform this adjustment, we multiply by 10^(36 - 8 - underlyingDecimals)
        (uint80 collateralBaseRoundID, int256 rawCollateralPrice, , uint256 collateralBaseTimestamp, uint80 collateralBaseAnsweredInRound) = strategy.collateralPriceOracle.latestRoundData();
        require(rawCollateralPrice > 0, "Collateral Price <= 0");
        require(collateralBaseAnsweredInRound >= collateralBaseRoundID , "Collateral Stale price");
        require(collateralBaseTimestamp > 0, "Collateral round not complete");
        rebalanceInfo.collateralPrice = rawCollateralPrice.toUint256().mul(10 ** strategy.collateralDecimalAdjustment);
        (uint80 borrowBaseRoundID, int256 rawBorrowPrice, , uint256 borrowBaseTimestamp, uint80 borrowBaseAnsweredInRound) = strategy.borrowPriceOracle.latestRoundData();
        require(rawBorrowPrice > 0, "Borrow Price <= 0");
        require(borrowBaseAnsweredInRound >= borrowBaseRoundID , "Borrow Stale price");
        require(borrowBaseTimestamp > 0, "Borrow round not complete");
        rebalanceInfo.borrowPrice = rawBorrowPrice.toUint256().mul(10 ** strategy.borrowDecimalAdjustment);

        rebalanceInfo.collateralBalance = strategy.targetCollateralAToken.balanceOf(address(strategy.setToken));
        rebalanceInfo.borrowBalance = strategy.targetBorrowDebtToken.balanceOf(address(strategy.setToken));
        rebalanceInfo.collateralValue = rebalanceInfo.collateralPrice.preciseMul(rebalanceInfo.collateralBalance);
        rebalanceInfo.borrowValue = rebalanceInfo.borrowPrice.preciseMul(rebalanceInfo.borrowBalance);
        rebalanceInfo.setTotalSupply = strategy.setToken.totalSupply();

        return rebalanceInfo;
    }
```
