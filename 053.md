saidam017

medium

# `lever()` and `delever()` of `AaveV3LeverageModule` could revert if used collateral token has blacklist

## Summary

When `lever()` and `delever()` of `AaveV3LeverageModule` called, it will eventually call `_accrueProtocolFee()` to calculate the protocol fee and try to send the fee to the fee recipient. However, If the fee recipient is blocked from the token, the process will revert.

## Vulnerability Detail

`lever()` and `delever()` will call  `_accrueProtocolFee()` : 

https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/modules/v1/AaveV3LeverageModule.sol#L281
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/modules/v1/AaveV3LeverageModule.sol#L342


```solidity
...
        uint256 protocolFee = _accrueProtocolFee(_setToken, _collateralAsset, postTradeReceiveQuantity);
...
```

`_accrueProtocolFee()` will calculate the fee and call `payProtocolFeeFromSetToken()` to send the fee : 

https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/modules/v1/AaveV3LeverageModule.sol#L840-L846

```solidity
    function _accrueProtocolFee(ISetToken _setToken, IERC20 _receiveToken, uint256 _exchangedQuantity) internal returns(uint256) {
        uint256 protocolFeeTotal = getModuleFee(PROTOCOL_TRADE_FEE_INDEX, _exchangedQuantity);

        payProtocolFeeFromSetToken(_setToken, address(_receiveToken), protocolFeeTotal);

        return protocolFeeTotal;
    }
```

`payProtocolFeeFromSetToken()` will be called and try to send the token to fee recipient using `strictInvokeTransfer()` : 

https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/lib/ModuleBase.sol#L153-L157

```solidity
  function payProtocolFeeFromSetToken(
    ISetToken _setToken,
    address _token,
    uint256 _feeQuantity
  ) internal {
    if (_feeQuantity > 0) {
      // @audit - if fee recipient is blocked, this will revert
      _setToken.strictInvokeTransfer(_token, controller.feeRecipient(), _feeQuantity);
    }
  }
```

https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/lib/Invoke.sol#L89-L112

```solidity
    function strictInvokeTransfer(
        ISetToken _setToken,
        address _token,
        address _to,
        uint256 _quantity
    )
        internal
    {
        if (_quantity > 0) {
            // Retrieve current balance of token for the SetToken
            uint256 existingBalance = IERC20(_token).balanceOf(address(_setToken));

            Invoke.invokeTransfer(_setToken, _token, _to, _quantity);

            // Get new balance of transferred token for SetToken
            uint256 newBalance = IERC20(_token).balanceOf(address(_setToken));

            // Verify only the transfer quantity is subtracted
            require(
                newBalance == existingBalance.sub(_quantity),
                "Invalid post transfer balance"
            );
        }
    }
```

Then `invokeTransfer` will be triggered : 

https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/lib/Invoke.sol#L66-L78

```solidity
    function invokeTransfer(
        ISetToken _setToken,
        address _token,
        address _to,
        uint256 _quantity
    )
        internal
    {
        if (_quantity > 0) {
            bytes memory callData = abi.encodeWithSignature("transfer(address,uint256)", _to, _quantity);
            _setToken.invoke(_token, 0, callData);
        }
    }
```

And eventually Set Token will try to `invoke()` this call, but the call will revert : 

https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/SetToken.sol#L197-L212

```solidity
    function invoke(
        address _target,
        uint256 _value,
        bytes calldata _data
    )
        external
        onlyModule
        whenLockedOnlyLocker
        returns (bytes memory _returnValue)
    {
        _returnValue = _target.functionCallWithValue(_data, _value);

        emit Invoked(_target, _value, _data, _returnValue);

        return _returnValue;
    }
```


## Impact

If fee recipient is blocked, any set token in the protocol that used this token can't to `lever()` and `delever()`  process.

## Code Snippet

https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/modules/v1/AaveV3LeverageModule.sol#L281
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/modules/v1/AaveV3LeverageModule.sol#L342
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/modules/v1/AaveV3LeverageModule.sol#L840-L846
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/lib/ModuleBase.sol#L153-L157


## Tool used

Manual Review

## Recommendation

Consider to use pull over push method to manage the protocol fee.