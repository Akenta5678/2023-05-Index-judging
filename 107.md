tallo

medium

# If a module is removed by the controller after calling lock() it will result in a complete bricking of the SetToken contract

## Summary
Modules that call ```lock()``` in ```SetToken.sol``` will be unable to call ```unlock()``` if they are removed as a module by the controller. This results in all the functionality behind the ```whenLockedOnlyLocker``` modifier being permanently locked since only the locker has permission to call ```unlock()```.

## Vulnerability Detail
The ```lock()``` function inside ```SetToken.sol``` allows modules to be designated as the ```locker``` and lock certain important functionality until they call ```unlock()```.
```solidity
    function lock() external onlyModule {
        require(!isLocked, "Must not be locked");
        locker = msg.sender;
        isLocked = true;
    }
```

If the module were to be removed in ```Controller.sol``` by the owner after ```lock()``` was just called then the module will no longer be registered.
```solidity
    function removeModule(address _module) external onlyInitialized onlyOwner {
        require(isModule[_module], "Module does not exist");

        modules = modules.remove(_module);

        //@audit here isModule is set to false
        isModule[_module] = false;

        emit ModuleRemoved(_module);
    }
```
When the module is no longer registered, it is no longer able to call functions behind the onlyModule modifier since ```controller#isModule(msg.sender)``` will return false.
```solidity
    modifier onlyModule() {
        _validateOnlyModule();
        _;
    }

    function _validateOnlyModule() internal view {
       //..
        require(
            controller.isModule(msg.sender),
            "Module must be enabled on controller"
        );
    }

```
This means that ```unlock()``` can't be called by anybody since it requires both ```onlyModule``` to not revert and ```locker``` to be equal to ```msg.sender```

```solidity
    function unlock() external onlyModule {
        require(isLocked, "Must be locked");
        require(locker == msg.sender, "Must be locker");
        delete locker;
        isLocked = false;
    }
```
## Impact
All key protocol functionality such as ```invoke()```, ```mint()```, ```burn()```, etc... will be blocked and rendered useless. Furthermore, simply re-adding and re-initializing the module to ```Controller.sol``` and ```SetToken.sol```  to call ```unlock()``` is not possible since ```initializeModule()``` requires that the protocol is not locked.

```solidity
//@audit SetToken.sol
   function initializeModule() external {
        require(!isLocked, "Only when unlocked");
```

## Code Snippet
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/SetToken.sol#L343
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/SetToken.sol#L684
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/SetToken.sol#L352
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/Controller.sol#L235

## Tool used

Manual Review

## Recommendation
Managers should have the ability to emergency unlock the protocol