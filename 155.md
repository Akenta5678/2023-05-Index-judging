Bauchibred

medium

# AaveLeverageStrategyExtension: Unnecessary Precision loss while delevering


## Summary

The `_calculateMinRepayUnits()` function, used in the AaveLeverageStrategyExtension contract's `_delever()` function, leads to unnecessary precision loss due to the order of operations. Performing division amidst a sequence of multiplications could cause precision loss due to possible rounding errors.

## Vulnerability Detail

During the execution of the `_delever()` function, an operation in `_calculateMinRepayUnits()` multiplies `_collateralRebalanceUnits` with `_actionInfo.collateralPrice`, divides the result by `_actionInfo.borrowPrice`, and multiplies again by the difference between `PreciseUnitMath.preciseUnit()` and `_slippageTolerance`. This sequence causes an unnecessary precision loss since a division operation is performed before completing all multiplication operations.

Take a look at both functions below to have a visual representation of the vulnerability

[\_delever()](https://github.com/sherlock-audit/2023-05-Index/blob/3190057afd3085143a31746d65045a0d1bacc78c/index-coop-smart-contracts/contracts/adapters/AaveLeverageStrategyExtension.sol#L772-L797)

```solidity

    /**
     * Calculate delever units Invoke delever on AaveLeverageModule.
     */
    function _delever(
        LeverageInfo memory _leverageInfo,
        uint256 _chunkRebalanceNotional
    )
        internal
    {
        uint256 collateralRebalanceUnits = _chunkRebalanceNotional.preciseDiv(_leverageInfo.action.setTotalSupply);

        uint256 minRepayUnits = _calculateMinRepayUnits(collateralRebalanceUnits, _leverageInfo.slippageTolerance, _leverageInfo.action);

        bytes memory deleverCallData = abi.encodeWithSignature(
            "delever(address,address,address,uint256,uint256,string,bytes)",
            address(strategy.setToken),
            strategy.collateralAsset,
            strategy.borrowAsset,
            collateralRebalanceUnits,
            minRepayUnits,
            _leverageInfo.exchangeName,
            exchangeSettings[_leverageInfo.exchangeName].deleverExchangeData
        );

        invokeManager(address(strategy.leverageModule), deleverCallData);
    }

```

[\_calculateMinRepayUnits()](https://github.com/sherlock-audit/2023-05-Index/blob/3190057afd3085143a31746d65045a0d1bacc78c/index-coop-smart-contracts/contracts/adapters/AaveLeverageStrategyExtension.sol#L1141-L1152)

```solidity
    /**
     * Derive the min repay units from collateral units for delever. Units are calculated as target collateral rebalance units multiplied by slippage tolerance
     * and pair price (collateral oracle price / borrow oracle price). Output is measured in borrow unit decimals.
     *
     * return uint256           Min position units to repay in borrow asset
     */
    function _calculateMinRepayUnits(uint256 _collateralRebalanceUnits, uint256 _slippageTolerance, ActionInfo memory _actionInfo) internal pure returns (uint256) {

    //@audit the division should be done as the last execution
        return _collateralRebalanceUnits
            .preciseMul(_actionInfo.collateralPrice)
            .preciseDiv(_actionInfo.borrowPrice)
            .preciseMul(PreciseUnitMath.preciseUnit().sub(_slippageTolerance));
    }
```

## Impact

This unnecessary precision loss might cause inaccuracies in the calculation of `minRepayUnits`, which could potentially affect the deleveraging process and the overall strategy operation.

## Code Snippet

[\_delever()](https://github.com/sherlock-audit/2023-05-Index/blob/3190057afd3085143a31746d65045a0d1bacc78c/index-coop-smart-contracts/contracts/adapters/AaveLeverageStrategyExtension.sol#L772-L797)

[\_calculateMinRepayUnits()](https://github.com/sherlock-audit/2023-05-Index/blob/3190057afd3085143a31746d65045a0d1bacc78c/index-coop-smart-contracts/contracts/adapters/AaveLeverageStrategyExtension.sol#L1141-L1152)

## Tool used

Manual Review

## Recommendation

To address this issue, consider modifying the `_calculateMinRepayUnits()` function to perform all multiplication operations before division, like this:

```solidity
    return _collateralRebalanceUnits
        .preciseMul(_actionInfo.collateralPrice)
        .preciseMul(PreciseUnitMath.preciseUnit().sub(_slippageTolerance))
        .preciseDiv(_actionInfo.borrowPrice);
```

This adjustment minimizes precision loss by reducing the impact of rounding errors caused by division operations.
