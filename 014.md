0xGoodess

medium

# _calculateMaxBorrowCollateral does not check whether the strategy is in Emode or not

## Summary
_calculateMaxBorrowCollateral does not check whether the strategy is in Emode or not

## Vulnerability Detail
While there is integration of aave Emode through the new extension `AaveLeverageStrategyExtension`, the existing function `_calculateMaxBorrowCollateral` does not integrate with this new feature.

eMode would increase the LTV and liquidation threshold of the strategy. if the strategy is set to eMode, with the function `_calculateMaxBorrowCollateral` not incorporating the new configuration, the system would behave as if without the emode being enabled.

```solidity
    function _calculateMaxBorrowCollateral(ActionInfo memory _actionInfo, bool _isLever) internal view returns(uint256) {
        
        // Retrieve collateral factor and liquidation threshold for the collateral asset in precise units (1e16 = 1%)
        ( , uint256 maxLtvRaw, uint256 liquidationThresholdRaw, , , , , , ,) = strategy.aaveProtocolDataProvider.getReserveConfigurationData(address(strategy.collateralAsset));

        // Normalize LTV and liquidation threshold to precise units. LTV is measured in 4 decimals in Aave which is why we must multiply by 1e14
        // for example ETH has an LTV value of 8000 which represents 80%
        if (_isLever) {
            uint256 netBorrowLimit = _actionInfo.collateralValue
                .preciseMul(maxLtvRaw.mul(10 ** 14))
                .preciseMul(PreciseUnitMath.preciseUnit().sub(execution.unutilizedLeveragePercentage));

            return netBorrowLimit
                .sub(_actionInfo.borrowValue)
                .preciseDiv(_actionInfo.collateralPrice);
        } else {
            uint256 netRepayLimit = _actionInfo.collateralValue
                .preciseMul(liquidationThresholdRaw.mul(10 ** 14))
                .preciseMul(PreciseUnitMath.preciseUnit().sub(execution.unutilizedLeveragePercentage));

            return _actionInfo.collateralBalance
                .preciseMul(netRepayLimit.sub(_actionInfo.borrowValue))
                .preciseDiv(netRepayLimit);
        }
    }
```
## Impact
wrong LTV and liquidation threshold might be used.

## Code Snippet
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-coop-smart-contracts/contracts/adapters/AaveLeverageStrategyExtension.sol#L1095-L1119

## Tool used

Manual Review

## Recommendation
add checks on whether the setToken is in eMode by calling `getUserEmode`, if so fetch the eModeConfiguration through`getEmodeCategoryData` with argument = 1. (ETH stETH eMode). If not in emode, follow the existing path of `getReserveConfigurationData`
