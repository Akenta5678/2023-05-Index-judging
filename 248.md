tallo

high

# ```ClaimModule``` does not update the position which leads to the accumulated rewards being lost

## Summary
```ClaimModule``` contains functionality to accumulate rewards through the ```_claim``` function. This function does not properly update the setTokens position as other modules such as ```AirdropModule``` do. This leads to the accumulated/claimed rewards not incrementing the position value through ```position#editDefaultPosition```.  

## Vulnerability Detail
inside ```ClaimModule#_claim``` the functionality to claim rewards is implemented. The function calculates the before and after balance of the ```rewardsToken``` for the passed SetToken before emitting an event and returning without calling ```_setToken.editDefaultPosition``` 
```solidity
    function _claim(ISetToken _setToken, address _rewardPool, string calldata _integrationName) internal {
        require(isRewardPool(_setToken, _rewardPool), "RewardPool not present");
        IClaimAdapter adapter = _getAndValidateIntegrationAdapter(_setToken, _rewardPool, _integrationName);

        IERC20 rewardsToken = IERC20(adapter.getTokenAddress(_rewardPool));
        
        //@audit rewards token before balance
        uint256 initRewardsBalance = rewardsToken.balanceOf(address(_setToken));
        

        (
            address callTarget,
            uint256 callValue,
            bytes memory callByteData
        ) = adapter.getClaimCallData(
            _setToken,
            _rewardPool
        );
        //@audit calls from the context of _setToken the protocol specific claim function such as claimComp for compound.
        _setToken.invoke(callTarget, callValue, callByteData);

        //@audit calculate the final balance of the rewards token after claiming
        //the total rewards received will be finalRewardsBalance-initRewardsBalance
        uint256 finalRewardsBalance = rewardsToken.balanceOf(address(_setToken));

        //@audit here the position should be updated 
        //..

        emit RewardClaimed(_setToken, _rewardPool, adapter, finalRewardsBalance.sub(initRewardsBalance));
    }
```

## Impact
The position is what keeps track of the amount of tokens held for a specific component and is used when calling ```DebtIssuanceModule#redeem``` so that the setToken manager can redeem their SetToken shares for the underyling assets. The ```DebtIssuanceModule#redeem``` function makes further function calls to ```DebtIssuanceModule#_calculateRequiredComponentIssuanceUnits``` and ```DebtIssuanceModule#_getTotalIssuanceUnits``` which is where the individual position values are summed up to get the total position value. This value won't include the accumulated rewards, so the rewards will be trapped in the contract. 


## Code Snippet
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/modules/v1/DebtIssuanceModule.sol#L191
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/modules/v1/DebtIssuanceModule.sol#L495
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/modules/v1/ClaimModule.sol#L375

## Tool used

Manual Review

## Recommendation
The include functionality that edits the default position by calling ```_setToken.editDefaultPosition``` to take into account the newly accrued tokens. Airdrop for instance correctly updates the position to account for the recently airdropped tokens.
```solidity
    function _handleAirdropPosition(ISetToken _setToken, IERC20 _token) internal {
        uint256 preFeeTokenBalance = _token.balanceOf(address(_setToken));
        uint256 amountAirdropped = preFeeTokenBalance.sub(_setToken.getDefaultTrackedBalance(address(_token)));

        if (amountAirdropped > 0) {
            (uint256 managerTake, uint256 protocolTake, uint256 totalFees) = _handleFees(_setToken, _token, amountAirdropped);

            uint256 newUnit = _getPostAirdropUnit(_setToken, preFeeTokenBalance, totalFees);

            _setToken.editDefaultPosition(address(_token), newUnit);

            emit ComponentAbsorbed(_setToken, _token, amountAirdropped, managerTake, protocolTake);
        }
    }
```

Note that this method can still lead to a griefing attack on rewards. Since certain rewards pools such as ```comptroller#claimComp``` implement the claim function publicly, its possible for anybody to call the function for anybody else. This means the rewards would still be sent without updating the setToken and the same issue above would occur. A workaround to this would be to include additional functionality that tracks setTokens individual token balances to see if there are inconsistencies between that and componentPositions virtualUnits.