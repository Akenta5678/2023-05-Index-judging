tallo

medium

# ```StreamingFeeModule#accrueFee``` can be DOS'd under certain conditions

## Summary
The ```StreamingFeeModule#accrueFee``` function can permanently revert and all fee important StreamingFeeModule.sol functionality to be locked.  This happens when enough time passes without ```accrueFee``` being called.
## Vulnerability Detail
The issue happens when ```accrueFee``` calls ```_calculateStreamingFee```

```solidity
    function accrueFee(ISetToken _setToken) public nonReentrant onlyValidAndInitializedSet(_setToken) {
        uint256 managerFee;
        uint256 protocolFee;

        if (_streamingFeePercentage(_setToken) > 0) {
            uint256 inflationFeePercentage = _calculateStreamingFee(_setToken);

            uint256 feeQuantity = _calculateStreamingFeeInflation(_setToken, inflationFeePercentage);
```
```_calculateStreamingFee``` first calculates the time elapsed since the fee was last updated; which initially happens when the contract is first initialized and also when ```accrueFee``` is called. This value is then multiplied by the ```streamingFeePercentage``` and divided by the amount of seconds in a year in order to get the proportion of the fee that is to be paid. For instance, if only half a year passed then only half the ```_streamingFeePercentage``` value will be calculated. It is possible for this proportion to be greater then 1 if more than a year has passed since the last time ```_lastStreamingFeeTimstamp``` was updated, and that is where the issue lies.

```solidity
    function _calculateStreamingFee(ISetToken _setToken) internal view returns(uint256) {
        uint256 timeSinceLastFee = block.timestamp.sub(_lastStreamingFeeTimestamp(_setToken));

        // Streaming fee is streaming fee times years since last fee
        return timeSinceLastFee.mul(_streamingFeePercentage(_setToken)).div(ONE_YEAR_IN_SECONDS);
    }
```
When ```_calculateStreamingFeeInflation``` is called with the previously calculated value from ```_calculateStreamingFee``` its possible for it to permanently revert when calculating the divisor ```b``` due to a subtraction underflow. This can happen if the ```_feePercentage``` or ```timeSinceLastFee*_streamingFeePercentage/ONE_YEAR_IN_SECONDS``` is greater than ```PreciseUnitMath.preciseUnit``` which is ```10e18```.  At a ```_streamingFeePercentage``` of 100% (10e18) this would take 1 year and at 33% (```3.33e18```) this would take 3 years without being updated.

```solidity
library PreciseUnitMath {
    uint256 constant internal PRECISE_UNIT = 10 ** 18;

    function preciseUnit() internal pure returns (uint256) {
        return PRECISE_UNIT;
    }
}

    function _calculateStreamingFeeInflation(
        ISetToken _setToken,
        uint256 _feePercentage
    )
        internal
        view
        returns (uint256)
    {
        uint256 totalSupply = _setToken.totalSupply();

        uint256 a = _feePercentage.mul(totalSupply);

        //@audit here it will revert if _feePercentage > preciseUnit which equals 10e18
        uint256 b = PreciseUnitMath.preciseUnit().sub(_feePercentage);

        return a.div(b);
    }
```
```solidity
    function initialize(
        ISetToken _setToken,
        FeeState memory _settings
    )
        external
        onlySetManager(_setToken, msg.sender)
        onlyValidAndPendingSet(_setToken)
    {
        require(_settings.feeRecipient != address(0), "Fee Recipient must be non-zero address.");
        require(_settings.maxStreamingFeePercentage < PreciseUnitMath.preciseUnit(), "Max fee must be < 100%.");
        require(_settings.streamingFeePercentage <= _settings.maxStreamingFeePercentage, "Fee must be <= max.");

        _settings.lastStreamingFeeTimestamp = block.timestamp;

        feeStates[_setToken] = _settings;
        _setToken.initializeModule();
    }
```


## Impact
All main fee accrual functionality in ```StreamingFeeModule``` will be frozen. Any attempt to call ```accrueFee``` and ```updateStreamingFee``` will result in a revert.

## Code Snippet
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/modules/v1/StreamingFeeModule.sol#L86
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/modules/v1/StreamingFeeModule.sol#L203
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/modules/v1/StreamingFeeModule.sol#L224
## Tool used

Manual Review

## Recommendation
Inside ```_calculateStreamingFeeInflation```, consider setting a cap on the _feePercentage variable so it doesnt cause a revert due to underflow 