rvierdiiev

medium

# AaveLeverageStrategyExtension._calculateMaxBorrowCollateral function should not use unutilizedLeveragePercentage when delever to 0

## Summary
AaveLeverageStrategyExtensionÑŽ_calculateMaxBorrowCollateral function should not use unutilizedLeveragePercentage when delever to 0.
## Vulnerability Detail
When operator wants to close strategy, then he can call `disengage` function.
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-coop-smart-contracts/contracts/adapters/AaveLeverageStrategyExtension.sol#L412-L438
```solidity
    function disengage(string memory _exchangeName) external onlyOperator {
        LeverageInfo memory leverageInfo = _getAndValidateLeveragedInfo(
            execution.slippageTolerance,
            exchangeSettings[_exchangeName].twapMaxTradeSize,
            _exchangeName
        );

        uint256 newLeverageRatio = PreciseUnitMath.preciseUnit();

        (
            uint256 chunkRebalanceNotional,
            uint256 totalRebalanceNotional
        ) = _calculateChunkRebalanceNotional(leverageInfo, newLeverageRatio, false);

        if (totalRebalanceNotional > chunkRebalanceNotional) {
            _delever(leverageInfo, chunkRebalanceNotional);
        } else {
            _deleverToZeroBorrowBalance(leverageInfo, totalRebalanceNotional);
        }

        emit Disengaged(
            leverageInfo.currentLeverageRatio,
            newLeverageRatio,
            chunkRebalanceNotional,
            totalRebalanceNotional
        );
    }
```

As you can see, this function calculates `chunkRebalanceNotional` and `totalRebalanceNotional` in order to undrstand if it's possible to delever to 0 in one call. In such case `_deleverToZeroBorrowBalance` function will be called. Otherwise operator should call `disengage` another time.

`_calculateChunkRebalanceNotional` function [calls `_calculateMaxBorrowCollateral`](https://github.com/sherlock-audit/2023-05-Index/blob/main/index-coop-smart-contracts/contracts/adapters/AaveLeverageStrategyExtension.sol#L1070) to find how many collateral asset it can use for one call.
https://github.com/sherlock-audit/2023-05-Index/blob/main/index-coop-smart-contracts/contracts/adapters/AaveLeverageStrategyExtension.sol#L1095-L1119
```solidity
    function _calculateMaxBorrowCollateral(ActionInfo memory _actionInfo, bool _isLever) internal view returns(uint256) {
        
        // Retrieve collateral factor and liquidation threshold for the collateral asset in precise units (1e16 = 1%)
        ( , uint256 maxLtvRaw, uint256 liquidationThresholdRaw, , , , , , ,) = strategy.aaveProtocolDataProvider.getReserveConfigurationData(address(strategy.collateralAsset));

        // Normalize LTV and liquidation threshold to precise units. LTV is measured in 4 decimals in Aave which is why we must multiply by 1e14
        // for example ETH has an LTV value of 8000 which represents 80%
        if (_isLever) {
            uint256 netBorrowLimit = _actionInfo.collateralValue
                .preciseMul(maxLtvRaw.mul(10 ** 14))
                .preciseMul(PreciseUnitMath.preciseUnit().sub(execution.unutilizedLeveragePercentage));

            return netBorrowLimit
                .sub(_actionInfo.borrowValue)
                .preciseDiv(_actionInfo.collateralPrice);
        } else {
            uint256 netRepayLimit = _actionInfo.collateralValue
                .preciseMul(liquidationThresholdRaw.mul(10 ** 14))
                .preciseMul(PreciseUnitMath.preciseUnit().sub(execution.unutilizedLeveragePercentage));

            return _actionInfo.collateralBalance
                .preciseMul(netRepayLimit.sub(_actionInfo.borrowValue))
                .preciseDiv(netRepayLimit);
        }
    }
```

As you can see, to find the maximum amount of collateral assets that can be traded to receive repayment token, first `collateralValue` is multiplied by `liquidationThresholdRaw` and then by `unutilizedLeveragePercentage`.
However in case if operator wants to fully delever to 0, then this `unutilizedLeveragePercentage` should not be counted. Because of this buffer, it's possible that operator will not be able to delever to 0 using one call, even when he should be able to do that.

Example:
1.SetToken has 1000$ collateral and 400$ debt, `unutilizedLeveragePercentage == 10%`, `liquidationThresholdRaw == 85%`.
2.`netRepayLimit` = 1000*85%*90%=765.
3.`netRepayLimit.sub(_actionInfo.borrowValue)` = 765-400=365, which is not eniugh to repay 400$ debt
4.however if you don't use `unutilizedLeveragePercentage` variable here, then `netRepayLimit` = 850 and `netRepayLimit.sub(_actionInfo.borrowValue)` = 850-400=450 and it's enough to delever to 0 in one call.
## Impact
Operator needs more time to call delever to 0.
## Code Snippet
Provided above
## Tool used

Manual Review

## Recommendation
In case of `disengage` you need to calculate if it's possible to delever to 0 for 1 time call. For this calculation you don't need to preserve `unutilizedLeveragePercentage`.